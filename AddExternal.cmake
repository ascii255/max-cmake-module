cmake_minimum_required(VERSION 3.25 FATAL_ERROR)

set(CMAKE_XCODE_GENERATE_SCHEME TRUE)
cmake_path(GET CMAKE_CURRENT_LIST_FILE PARENT_PATH CURRENT_MODULE_DIR)

function(add_external TARGET EXTERNAL_NAME)
    cmake_parse_arguments(ARG "" "VERSION;REVERSE_DOMAIN;COPYRIGHT" "" ${ARGN})
    if(ARG_VERSION)
        set(PACKAGE_VERSION ${ARG_VERSION})
    endif()
    if(ARG_REVERSE_DOMAIN)
        set(PACKAGE_REVERSE_DOMAIN ${ARG_REVERSE_DOMAIN})
    endif()
    if(ARG_COPYRIGHT)
        set(PACKAGE_COPYRIGHT ${ARG_COPYRIGHT})
    endif()

    set(PRODUCT_IDENTIFIER ${EXTERNAL_NAME})
    string(REPLACE ~ - PRODUCT_IDENTIFIER ${PRODUCT_IDENTIFIER})
    set(BUNDLE_IDENTIFIER ${PACKAGE_REVERSE_DOMAIN}.${PRODUCT_IDENTIFIER})

    add_library(${TARGET} MODULE)

    set_target_properties(${TARGET} PROPERTIES
        BUNDLE TRUE
        BUNDLE_EXTENSION mxo
        LIBRARY_OUTPUT_DIRECTORY ${CMAKE_SOURCE_DIR}/externals
        MACOSX_BUNDLE_INFO_PLIST ${CURRENT_MODULE_DIR}/ExternalInfo.plist.in
        MACOSX_BUNDLE_BUNDLE_NAME ${EXTERNAL_NAME}
        MACOSX_BUNDLE_BUNDLE_VERSION ${PACKAGE_VERSION}
        MACOSX_BUNDLE_COPYRIGHT ${PACKAGE_COPYRIGHT}
        MACOSX_BUNDLE_GUI_IDENTIFIER ${BUNDLE_IDENTIFIER}
        MACOSX_BUNDLE_LONG_VERSION_STRING "${EXTERNAL_NAME} ${PACKAGE_VERSION} - ${PACKAGE_COPYRIGHT}"
        MACOSX_BUNDLE_SHORT_VERSION_STRING ${PACKAGE_VERSION}
        OSX_ARCHITECTURES "arm64;x86_64"
        OUTPUT_NAME ${EXTERNAL_NAME}
        PREFIX ""
        XCODE_ATTRIBUTE_CONFIGURATION_BUILD_DIR ${CMAKE_SOURCE_DIR}/externals
        XCODE_ATTRIBUTE_ENABLE_HARDENED_RUNTIME YES
        XCODE_ATTRIBUTE_PRODUCT_BUNDLE_IDENTIFIER ${BUNDLE_IDENTIFIER}
        XCODE_ATTRIBUTE_PRODUCT_MODULE_NAME ${EXTERNAL_NAME}
        XCODE_ATTRIBUTE_WRAPPER_EXTENSION mxo
        XCODE_SCHEME_EXECUTABLE /Applications/Max.app
        XCODE_SCHEME_ARGUMENTS "\"\${PROJECT_DIR}/help/${EXTERNAL_NAME}.maxhelp\""
    )
endfunction()
